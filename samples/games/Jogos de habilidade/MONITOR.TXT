100 REM
110 REM **** MONITOR ASSEMBLER ****
120 REM         de Wilson F. Martins
130 REM
140 KEYOFF:CLEAR 1000:DEFINT A-Z
150 DEFFNH$(A)=RIGHT$("0"+HEX$(A),2)
160 DEFFNI$(A)=RIGHT$("000"+HEX$(A),4)
170 CLS:PRINT"** MONITOR ASSEMBLER **"
180 PRINT
190 INPUT"Endere‡o inicial ";E$
200 E=VAL("&H"+E$):CLS:L=-1:ML=L
210 L=L+1:IF L>23 THEN L=23
220 EE=INT(E/8)*8:PRINT FNI$(EE);" - ";
230 EE=INT(E/8)*8:FORN=0TO7:PRINT FNH$(PEEK(EE+N));" ";
240 NEXT:C=E-EE
250 GOSUB 480
260 AL=1
270 LOCATE 7+C*3,L,1
280 I$=INKEY$:IF I$="" THEN 280
290 I=STICK(0):IF I THEN 420
300 IF ASC(I$)=24 THEN 170
310 IF ASC(I$)=27 THEN 510
320 IF ASC(I$)>96 AND ASC(I$)<103 THEN I$=CHR$(ASC(I$)-32)
330 IF I$<"0" OR I$>"9" AND I$<"A" OR I$>"F" THEN 260
340 PRINT I$;:B$(AL)=I$
350 AL=AL-1:IF AL>-1 THEN 280
360 B=VAL("&H"+B$(1)+B$(0))
370 POKE E,B
380 GOSUB 480
390 E=E+1
400 C=C+1:IF C=8 THEN PRINT:GOTO 210
410 GOTO 260
420 IF I=7 THEN C=C-1:E=E-1
430 IF I=3 THEN 390
440 IF I=1 AND L>0 THEN E=E-8:L=L-1
450 IF I=5 THEN E=E+8:PRINT:GOTO 210
460 IF C<0 THEN IF L>0 THEN C=7:L=L-1 ELSE C=C+1:E=E+1
470 GOTO 260
480 S=0:EE=INT(E/8)*8:FORN=0TO7:S=S+PEEK(EE+N)*(N+1)
490 NEXT:LOCATE 32,CSRLIN:PRINT"  ";FNH$(S);
500 RETURN
510 CLS:INPUT "Nome do programa ";N$
520 FOR N=0TO2:READ X$
530 PRINT "Endere‡o ";X$;:INPUT E$:E(N)=VAL("&H"+E$)
540 NEXT
550 BSAVE ""+N$,E(0),E(1),E(2)
560 RUN
570 DATA inicial,final,de entrada
