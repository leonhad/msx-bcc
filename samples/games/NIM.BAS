10 SCREEN 0:CLS
20 KEY OFF
30 COLOR 15,0,0
40 DIM A(5),B(5)
50 PRINT "Regas:":PRINT
60 PRINT " Cada jogador pega quantos palitos"
70 PRINT " quiser da mesma fila."
80 PRINT
90 PRINT " Cada um joga uma vez."
100 PRINT
110 PRINT " Ganha quem pegar o £ltimo palito."
120 FOR J=1 TO 500:NEXT
130 T=0
140 FOR J=1 TO 4
150 A(J)=INT(RND(-TIME)*7):T=T+A(J)
160 NEXT J
170 IF T<4 THEN 130
180 FOR J=1 TO 4
190 B(J)=A(J)
200 NEXT J
210 M=0:M0=0
220 PRINT
230 T6=0
240 FOR J=1 TO 4
250 PRINT TAB(10):PRINT CHR$(J+64);" :";
260 FOR K=1 TO 9
270 C=198:IF K>A(J) THEN C=32
280 PRINT CHR$(C);" ";
290 NEXT K
300 PRINT:PRINT
310 IF A(J)>T6 THEN T6=A(J):T5=J
320 NEXT J
330 IF M>0 THEN 380
340 PRINT "Quer jogar primeiro (S/N)?";
350 X$=INPUT$(1)
360 IF X$="S" OR X$="s" THEN M0=1:GOTO 380
370 IF X$<>"N" AND X$<> "n" THEN GOTO 350
380 M=M+1:IF M0=1 THEN 520
390 T=0
400 FOR J=1 TO 4
410 T=T XOR A(J)
420 NEXT J
430 J1=T5:X=T6
440 IF T=0 THEN X=1:GOSUB 930:GOTO 490
450 FOR J=1 TO 4
460 T0=T XOR A(J)
470 IF T0<A(J) THEN J1=J:X=A(J)-T0
480 NEXT J
490 PRINT:PRINT "Eu tiro";X;"da fila ";CHR$(J1+64)
500 PRINT
510 GOTO 630
520 PRINT:PRINT "Escolha uma linha"
530 PRINT "ou aperte <ESC> para desistir"
540 X$=INPUT$(1):X$=CHR$(ASC(X$) AND 223)
550 IF ASC(X$)=27 THEN M0=1:GOTO 730
560 J1=ASC(X$)-64:IF J1<1 OR J1>4 THEN 540
570 IF A(J1)=0 THEN 540
580 PRINT "Quantos palitos da linha ";CHR$(64+J1);": ";
590 X$=INPUT$(1)
600 X=ASC(X$)-48:IF X<0 OR X>9 THEN 590
610 IF X=0 OR X>A(J1) THEN 590
620 PRINT X
630 PRINT
640 A(J1)=A(J1)-X
650 T=0
660 FOR J=1 TO 4
670 IF A(J)<>0 THEN T=1
680 NEXT J
690 M0=1-M0
700 IF T=0 THEN 730
710 FOR J=1 TO 100:NEXT J
720 GOTO 230
730 PRINT
740 IF M0=1 THEN W0=W0+1:PRINT "Ganhei!"
750 IF M0=0 THEN W1=W1+1:PRINT "Vocˆ ganhou!"
760 PRINT:PRINT
770 PRINT TAB(5):PRINT "At‚ agora eu ganhei!";W0;"partida";
780 IF W<>1 THEN PRINT"s" ELSE PRINT
790 PRINT
800 PRINT TAB(11):PRINT"e vocˆ ganhou";W1
810 PRINT:PRINT
820 PRINT"Quer jogar de novo (S/N)?"
830 X$=INPUT$(1)
840 IF X$="N" OR X$="n" THEN END
850 IF X$<>"S" AND X$<>"s" THEN GOTO 830
860 PRINT "Mesmo jogo (S/N)?"
870 X$=INPUT$(1)
880 IF X$="S" OR X$="s" THEN 910
890 IF X$<>"N" AND X$<>"n" THEN 870
900 GOTO 120
910 FOR J=1 TO 4:A(J)=B(J):NEXT J
920 GOTO 210
930 J1=INT(RND(-TIME)*4)+1
940 X=INT(RND(-TIME)*6)+1
950 IF X>A(J1) THEN X=X-1:GOTO 950
960 IF X=0 THEN 930
970 RETURN
