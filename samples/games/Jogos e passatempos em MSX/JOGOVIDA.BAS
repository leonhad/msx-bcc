10 REM <<<< Jogo da vida >>>>
20 KEY OFF
30 DIM H(18,18),M(687)
40 NC=17:NL=17
50 FOR C=1 TO NC+1
60 FOR L=1 TO NL+1
70 H(C,L)=0
80 NEXT L:NEXT C
90 CLS
100 FOR A=1 TO NC
110 FOR B=1 TO NL
120 LOCATE 9+A,2+B
130 PRINT CHR$(219)
140 NEXT B:NEXT A
150 C=1:L=1
160 IF H(C,L)=0 THEN LOCATE 9+C,2+L:PRINT " ":LOCATE 9+C,2+L:PRINT CHR$(219)
170 IF H(C,L)=1 THEN LOCATE 9+C,2+L:PRINT " ":LOCATE 9+C,2+L:PRINT CHR$(249)
180 A$=INKEY$
190 IF A$="" THEN 160
200 K=ASC(A$)
210 C=C+(K=29)-(K=28)
220 L=L+(K=30)-(K=31)
230 IF C<1 THEN C=1
240 IF C>NC THEN C=NC
250 IF L<1 THEN L=1
260 IF L>NL THEN L=NL
270 IF K=32 THEN H(C,L)=1
280 IF K=13 THEN H(C,L)=0
290 IF K=27 THEN GOTO 310
300 GOTO 160
310 CLS
320 LOCATE 1,21
330 PRINT "Gera‡±o: 1"
340 FOR C=1 TO NC
350 FOR L=1 TO NL
360 TT=H(C-1,L-1)+H(C,L-1)+H(C+1,L-1)+H(C-1,L)+H(C+1,L)+H(C-1,L+1)+H(C,L+1)+H(C+1,L+1)
370 IF H(C,L)=0 THEN LOCATE 9+C,2+L:PRINT CHR$(219)
380 IF H(C,L)=0 AND TT=3 THEN LOCATE 9+C,2+L:PRINT CHR$(249):I=I+3:M(I-2)=C:M(I-1)=L:M(I)=1
390 IF H(C,L)=1 THEN LOCATE 9+C,2+L:PRINT CHR$(249)
400 IF H(C,L)=1 AND (TT<2 OR TT>3) THEN LOCATE 9+C,2+L:PRINT CHR$(219):I=I+3:M(I-2)=C:M(I-1)=L:M(I)=0
410 NEXT L:NEXT C
420 IF I=0 THEN 450
430 FOR A=1 TO I STEP 3:H(M(A),M(A+1))=M(A+2):NEXT A
440 I=0:G=G+1:CLS:LOCATE 1,21:PRINT "Gera‡±o:";G+1:GOTO 340
450 LOCATE 15,21:PRINT "Forma est vel"
